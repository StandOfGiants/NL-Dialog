title: date_helmeppo_3_main
location: day(office)
characters: [helmeppo]
---
<<declare $hd3_points = 0>>
As your work day nears its end, you see Helmeppo walking toward you, eyes on a paper in his hands. But as he nears you, he looks up, then smiles.
Helmeppo: Hello {$name}, I was actually looking for you. I’m heading into town to grab dinner tonight. Want to join me? My treat.
-> Yes, that sounds great!
    <<set $hd3_points += 1>>
    The two of you make plans to meet in an hour and part ways.
    <<jump date_helmeppo_3_WalkOver>>
-> I’m not sure…
    Helmeppo: It’s at Cangrejo Amarillo, if that matters.
    Cangrejo Amarillo. A super fancy restaurant. Its small footprint means limited seating, and its high prices mean most people at Headquarters hesitate to visit it very often.
    -> You know what? It does. Sign me up for free fancy food.
        <<set $hd3_points += 1>>
        Helmeppo: Heh. Consider yourself signed up.
        You make plans to meet after your shift.
        <<jump date_helmeppo_3_WalkOver>>
    -> Well, I’ve never been. It sounds fun, actually.
        <<set $hd3_points += 1>>
        Helmeppo: All right! It’ll be good to have company. Meet me in the park in an hour?
        You make plans to meet after your shift.
        <<jump date_helmeppo_3_WalkOver>>
    -> That sounds a little too rich for my blood, but thank you for the invitation.
        <<jump date_helmeppo_3_GoHome1>>
    -> It does. I’ll pass.
        <<set $hd3_points -= 1>>
        <<jump date_helmeppo_3_GoHome1>>
-> No, thank you.
    Helmeppo looks a little disappointed, but it’s gone in a moment and he shrugs.
    Helmeppo: Your loss. I was going to Cangrejo Amarillo.
    -> Wait, the super fancy Dressrosan restaurant. Can I change my mind?
        <<set $hd3_points += 1>>
        Helmeppo: For the next ten seconds, then I’m going without you.
        {$name}: Yes! Fancy dinner!
        Helmeppo chuckles, and you make an agreement to meet up in the park in an hour.
        <<jump date_helmeppo_3_WalkOver>>
    -> Well, I hope you enjoy your dinner.
        <<set $hd3_points -= 1>>
        <<jump date_helmeppo_3_GoHome1>>
===

title: date_helmeppo_3_WalkOver
location: day(park)
---
The two of you meet up in the park as the sun is setting and head over to the restaurant. It’s a beautiful spot, full of rich furnishings and beautiful decor. The greeter walks up as you enter and glances at you both.
<<show_characters helmeppo Greeter>>
Greeter: Good evening! Are you here on business? Or would you prefer a more intimate table?
The question seems to catch Helmeppo off guard, which means it’s up to you to answer.
-> Strictly business.
    Greeter: Understood.
    The greeter leads you to a table near the front of the restaurant, near the largest windows. This would give plenty of light for looking over papers or conducting other official business over a meal.
    As you’re seated and handed your menus, Helmeppo seems distracted by something.
    <<jump date_helmeppo_3_Convo1>>
-> We’re friends having dinner, what do you recommend for that?
    <<set $hd3_points += 1>>
    Greeter: I see.
    The greeter brings you to a table in the middle of the restaurant. It’s a little busy with all the servers and bussers, but it gives the place a lively feel.
    As you’re seated and handed your menus, Helmeppo seems distracted by something.
    <<jump date_helmeppo_3_Convo1>>
-> It’s a date, so…
    <<set $hd3_points += 1>>
    Greeter: I see!
    The greeter shows you to an out-of-the-way table near the back, where the lights are a little lower and there are more decorative candles on the tables.
    As you’re seated and handed your menus, Helmeppo seems surprisingly flustered.
    <<jump date_helmeppo_3_Convo1>>
===

title: date_helmeppo_3_Convo1
characters: [helmeppo]
---
It’s up to you to get the conversation going.
-> So why do you use kukri? They’re an unusual weapon.
    That seems to focus him. One of Helmeppo’s hands drifts to where his weapons are sheathed.
    Helmeppo: Oh! Um… No special reason. But anyone could tell when I started out that swords were [i]not[/i] right for me.
    -> That bad, huh?
        <<set $hd3_points += 1>>
        Helmeppo lets out a wry laugh.
        Helmeppo: Worse than you can even imagine. Bogard said I used them like I was trying to bludgeon the enemy into submission.
        {$name}: How does that even work?
        Helmeppo: That’s what [i]he[/i] asked too. Luckily, he tried me out on a few others, and the kukri just clicked.
        Helmeppo: How about you? Have you discovered your preferred weapon yet?
        -> Yes, I have!
            Helmeppo: Oh, which one?
            -> Sword
                Helmeppo: Tried and true. There’s only one thing to remember.
                -> Keep the blade honed?
                    Helmeppo: No. Remember a sword is not for bludgeoning.
                    <<jump date_helmeppo_3_OrderFood>>
                -> Pay attention to my footwork?
                    Helmeppo: No. Remember a sword is not for bludgeoning.
                    <<jump date_helmeppo_3_OrderFood>>
                -> It is not a bludgeoning weapon?
                    Helmeppo: Exactly!
                    <<jump date_helmeppo_3_OrderFood>>
            -> Gun
                Helmeppo: Interesting choice. You must have a good eye.
                -> Not really.
                    Helmeppo: Ah. So going for foolish bravery huh?
                    Helmeppo: Well, can’t say I’m not used to being around that. I’m sure you’ll do fine.
                    <<jump date_helmeppo_3_OrderFood>>
                -> Definitely.
                    Helmeppo: Impressive. Good choice, then.
                    <<jump date_helmeppo_3_OrderFood>>
            -> Knife
                Helmeppo nods approvingly.
                Helmeppo: Quick, versatile… Never a bad choice.
                Helmeppo: Just don’t let them tell you the standard issue ones are fine.
                Helmeppo: They are not fine. Get your own. Trust me.
                <<jump date_helmeppo_3_OrderFood>>
            -> Something a little unusual
                Helmeppo: Welcome to the club. Prepare for finding replacements to be a total pain.
                {$name}: That wasn’t a joke, huh.
                Helmeppo: Sadly, no.
                <<jump date_helmeppo_3_OrderFood>>
        -> I was hoping you would help me.
            Helmeppo: Oh really? I’m not sure I’m the best…
            He pauses, then hangs his head.
            Helmeppo: I actually offered, didn’t I?
            {$name}: Yeah, but if you don’t want-
            Helmeppo: No, I think that would be fun. Let’s see.
            <<jump date_helmeppo_3_WeaponSelect>>
        -> I’m probably fine with whatever.
            Helmeppo shakes his head.
            Helmeppo: Pirates aren’t playing around. You’ll want to pick something that... Oh.
            Helmeppo: Sorry. Not my place to be giving lectures.
            Helmeppo: But that offer still stands. If you want help picking one…
            <<jump date_helmeppo_3_WeaponSelect>>
    -> And they didn’t just make you keep trying?
        Helmeppo: I think the military is pretty good at recognizing a lost cause when they see one.
        He shrugs.
        Helmeppo: Also, old dog, new tricks.
        -> Good thing they stuck with it.
            Helmeppo: That’s true.
            Quiet falls again.
            <<jump date_helmeppo_3_OrderFood>>
        -> Good thing you worked so hard.
            <<set $hd3_points += 1>>
            Helmeppo lets out a snort of laughter, but he looks pleased.
            Helmeppo: I guess so. But you don’t get to reach your dreams by being lazy.
            {$name}: And what is your dream?
            Helmeppo: Well, getting to eat more food like what they serve here, for starters. Speaking of which, do you know what you’re going to order?
            -> I think so.
                Helmeppo: Good, because they’re coming to ask us now, I think.
                <<jump date_helmeppo_3_OrderFood>>
            -> Not yet.
                Helmeppo: Well, you better hurry up. I think they’re coming to take our order now.
                <<jump date_helmeppo_3_OrderFood>>
        -> You can do tricks with the kukri?
            <<set $hd3_points += 1>>
            Helmeppo: Oh, a few. Juggling. Disappearing and reappearing blades. Light murder. The usual.
            {$name}: Oooh, fancy. Teach me?
            Helmeppo: If I must. Though not tonight. Tonight is about eating a rare good meal at Marineford.
            {$name}: You don’t like mess hall food?
            Helmeppo: It’s not the worst. But this place? This is [i]food[/i]. Speaking of which…
            <<jump date_helmeppo_3_OrderFood>>
    -> But who even thought to try kukri?
        Helmeppo: Bogard. But he made me try a lot of things.
        {$name}: Oh yeah? What else?
        Helmeppo: Well, there were the kama. That was… Yeah, terrible. Utterly terrible.
        Helmeppo: And then a naginata. But a two-handed weapon just never felt right.
        Helmeppo: And then there was the squeaky hammer…
        -> I can’t believe that.
            Helmeppo: That’s good, because it was a joke.
            <<jump date_helmeppo_3_OrderFood>>
        -> And you didn’t stick with that last one?
            <<set $hd3_points += 1>>
            Helmeppo: Hard to imagine, I know. The kukri were truly a step backward.
            The statement is made so bluntly that even though you know he’s kidding, you half worry you might be wrong.
            {$name}: That is one incredibly dry sense of humor you have there.
            Helmeppo: Thanks. I cultivate it like a desert.
            <<jump date_helmeppo_3_OrderFood>>
        -> No one could lose you on a battlefield with that one.
            <<set $hd3_points += 1>>
            Helmeppo: Beware the squeak of doom.
            {$name}: Tremble before it?
            Helmeppo: You know it.
            <<jump date_helmeppo_3_OrderFood>>
-> Would you get another pet wolf if you had the chance?
    Helmeppo: Would I… Huh. You know, that’s a good question.
    {$name}: You haven’t given it any thought before?
    Helmeppo: Honestly? No. So much has happened since then, and frankly, the Marines doesn’t seem like a great place to keep a pet.
    Helmeppo: … Although maybe…
    -> Thinking you might get one now?
        Helmeppo: Hmmm… Maybe if I get to vice admiral.
        Helmeppo: At that point, I’ll probably need a therapy pet to keep me from stressing myself into a heart attack.
        -> Do you think you’ll get there?
            <<set $hd3_points += 1>>
            Helmeppo: Honestly, I don’t know.
            Helmeppo: How about you? How far do you want to climb the bureaucracy ladder?
            -> I’m happy wherever.
                Helmeppo: Easygoing, huh? You may survive the swamp of this place yet.
                <<jump date_helmeppo_3_OrderFood>>
            -> I’d like to have my own ship someday.
                He nods.
                Helmeppo: Well, I hope you get there.
                <<jump date_helmeppo_3_OrderFood>>
            -> I plan to make fleet admiral.
                He looks surprised.
                Helmeppo: I wish you luck. There’s no amount of money that could make me want that, but if it’s your dream, I hope you get there.
                <<jump date_helmeppo_3_OrderFood>>
        -> I advise buying a stress toy. I don’t think the Marines have regulations about those.
            <<set $hd3_points += 1>>
            He actually laughs briefly at that.
            Helmeppo: So noted. Maybe I can find one shaped like something friendly.
            -> A wolf?
                Helmeppo tilts his head to one side, thinking.
                Helmeppo: Maybe that’s the better way to go about it.
                <<jump date_helmeppo_3_OrderFood>>
            -> A label maker?
                Helmeppo grins.
                Helmeppo: I wasn’t thinking about it like that, but that wouldn’t be bad.
                <<jump date_helmeppo_3_OrderFood>>
            -> Coby?
                Helmeppo pales a little.
                Helmeppo: Not that I haven’t thought about squeezing him when he’s the one stressing me out but…
                <<jump date_helmeppo_3_OrderFood>>
        -> You? Vice admiral?
            <<set $hd3_points -= 1>>
            He gives you a thin, humorless smile.
            Helmeppo: Fair enough.
            <<jump date_helmeppo_3_OrderFood>>
    -> Think they’d let [i]me[/i] have a pet?
        <<set $hd3_points += 1>>
        He chuckles.
        Helmeppo: I imagine if it sounds like something good, they won’t allow it.
        Helmeppo: But what would you want if you could have one?
        -> I’d want a dog or cat.
            Helmeppo: That would be cute. But if you don’t take them on the ship with you, I imagine they’d get really lonely.
            He frowns a little.
            Helmeppo: And come to think of it… Very hungry too. I don’t think the base has a pet sitting service.
            -> Oh, yeah. That’s probably right.
                Helmeppo looks a little regretful for saying anything.
                Helmeppo: Well, I feel like if you really want one, you’ll figure something out.
                <<jump date_helmeppo_3_OrderFood>>
            -> I’d definitely take it on the boat!
                Helmeppo: You’re really gung ho about this.
                Helmeppo: Well, I hope you get that pet someday.
                <<jump date_helmeppo_3_OrderFood>>
            -> On second thought, maybe I don’t want a pet…
                He nods regretfully.
                Helmeppo: Yeah. Hate to say it, but the Marines is probably no place for a pet.
                <<jump date_helmeppo_3_OrderFood>>
        -> How about a horse?
            He laughs.
            Helmeppo: A horse fan, huh? Bold choice.
            Helmeppo: Why a horse though? If it’s not prying too much.
            -> To ride into battle.
                Helmeppo: Leading the charge? I’d feel bad for the horse, though.
                {$name}: Oh… That’s a good point.
                Helmeppo: At least it would get plenty of exercise, though?
                <<jump date_helmeppo_3_OrderFood>>
            -> Horses are awesome.
                He shrugs in surrender.
                Helmeppo: Yeah, I’ll have to give you that one. They seem pretty great. ⇒ OrderFood
            -> I loved horse-riding when I was younger.
                He nods.
                Helmeppo: Nostalgia, then? Like me and that wolf I guess.
                <<jump date_helmeppo_3_OrderFood>>
        -> Something a little more… Unique, I think.
            Helmeppo nods.
            Helmeppo: I can relate to that instinct. What were you thinking?
            -> An insect.
                He visibly flinches.
                Helmeppo: Is that so? I guess to each their own.
                <<jump date_helmeppo_3_OrderFood>>
            -> A lizard.
                He raises an eyebrow.
                Helmeppo: Well, on the bright side, you might be able to take that on a ship without too much problem.
                <<jump date_helmeppo_3_OrderFood>>
            -> A monster.
                He laughs.
                Helmeppo: You know, I might not be surprised if they let you do that. Imagine the looks on pirates’ faces.
                <<jump date_helmeppo_3_OrderFood>>
    -> Honestly, I think it’s a bad idea. You’re better off without one.
        <<set $hd3_points -= 1>>
        He frowns a little at that, then shrugs.
        Helmeppo: You’re starting to sound like some of our superior officers.
        -> And is that a bad thing?
            Helmeppo frowns out the window.
            Helmeppo: I’m going to plead the fifth on that.
            <<jump date_helmeppo_3_OrderFood>>
        -> Yeah, sorry. That was a little rude.
            <<set $hd3_points += 1>>
            Helmeppo: Nah, don’t worry about it. You’re not wrong. I just miss him sometimes.
            <<jump date_helmeppo_3_OrderFood>>
-> Are you hoping you’ll get your own ship someday?
    He looks surprised to be asked that.
    Helmeppo: Honestly? I’ve never really thought about it.
    -> You should. I think you’d make a good captain.
        <<set $hd3_points += 1>>
        He averts his eyes for a moment, and you swear you see just the barest hint of a blush.
        Helmeppo: Well, that’s your mistake, but thank you regardless.
        He clears his throat.
        Helmeppo: Oh! I think the waiter is coming over.
        <<jump date_helmeppo_3_OrderFood>>
    -> I bet having you for a leader would be interesting.
        <<set $hd3_points += 1>>
        He shakes his head.
        Helmeppo: Interesting, huh?
        Helmeppo: That’s one of those words when you want a polite way to say bad.
        -> Yeah. Sorry, but I don’t think it’s your strong suit.
            Helmeppo: No, I agree with you.
            Helmeppo: I’d need to apologize to anyone unfortunate enough to be trapped on that ship with me.
            -> There are worse fates.
                Helmeppo: Sure, but that doesn’t exactly make it sound good. Just not the worst.
                He shrugs.
                Helmeppo: Ah, well. I guess I’ll take it as a compliment.
                <<jump date_helmeppo_3_OrderFood>>
            -> Of course, you’d be trapped with them too…
                <<set $hd3_points += 1>>
                Helmeppo actually laughs.
                Helmeppo: A sort of hell for both sides, huh?
                Helmeppo: Well, not my idea of a good time, but I guess it might be entertaining to watch. At least until the ship went down in flames…
                <<jump date_helmeppo_3_OrderFood>>
        -> That wasn’t my intent.
            With a wry smile, he glances out the window before answering.
            Helmeppo: Maybe it should have been. I appreciate the vote of confidence, but I really wouldn’t be very good with my own command. It’s not what I want to do.
            -> Oh, OK.
                Helmeppo: Sorry. Didn’t make you feel bad.
                Helmeppo: I guess that’s just the natural me shining through…
                <<jump date_helmeppo_3_OrderFood>>
            -> What [i]do[/i] you want to do?
                He chuckles.
                Helmeppo: I’m not sure I want to share that just yet. But it definitely doesn’t involve my own ship.
                <<jump date_helmeppo_3_OrderFood>>
        -> Oh, you know that one, huh?
            <<set $hd3_points += 1>>
            Helmeppo: I’ve used it a time or two in my life.
            He chuckles.
            Helmeppo: Well… You’re probably right. It would be interesting, in the most sarcastic possible meaning of the word.
            Helmeppo: I’d be demoted before a pirate crew had time to put an end to us, I imagine.
            {$name}: Would it really be [i]that[/i] bad?
            Helmeppo: Honestly, probably worse.
            <<jump date_helmeppo_3_OrderFood>>
    -> Oh. Yeah. Forget I said anything.
        He smiles wryly.
        Helmeppo: Yeah. That’s probably for the best.
        <<jump date_helmeppo_3_OrderFood>>
-> Nothing. You stay silent as well.
    <<set $hd3_points -= 1>>
    <<jump date_helmeppo_3_OrderFood>>
===

title: date_helmeppo_3_WeaponSelect
---
Helmeppo: So, what sort of weapons were you considering?
-> Sword
    Helmeppo tilts his head a little to the left, thoughtfully.
    Helmeppo: I feel like if you’re asking this question, then it’s not the right weapon for you.
    Helmeppo: Are you enjoying the sword weapon drills?
    -> Yes.
        Helmeppo: Honestly, if you’ve gotten through all of those and still like messing with them, sounds like a match made in heaven. I bet Tashigi would help you out.
        <<jump date_helmeppo_3_OrderFood>>
    -> No.
        He winces.
        Helmeppo: So, then you should [i]definitely[/i] not go with a sword. Anything else you’re considering?
        <<jump date_helmeppo_3_WeaponSelect>>
-> Gun
    Helmeppo: Nice. Keep the enemy on the other side of the battlefield. I approve.
    Helmeppo: How good of a shot are you?
    -> Call me Bull’s-eye.
        He looks impressed.
        Helmeppo: Oh really? Impressive. I know most of the big pirate crews have sharpshooters. It’d be great if we had a few more on our side.
        <<jump date_helmeppo_3_OrderFood>>
    -> I generally hit the target.
        Helmeppo: Oh yeah? Well, if you like the idea of going with range, you’re probably going to want to bump that up just a little before committing.
        <<jump date_helmeppo_3_OrderFood>>
    -> Uhhh…
        As you search for a way to describe your aim that doesn’t sound awful, he shakes his head.
        Helmeppo: No offense, but … Think of something else.
        <<jump date_helmeppo_3_WeaponSelect>>
-> Something more unusual
    He looks impressed.
    Helmeppo: Going a little further afield huh? What direction were you thinking?
    -> Something with a long handle.
        Helmeppo: What, some sort of polearm?
        He falls silent for a few seconds.
        Helmeppo: That could be interesting, honestly. Make strike forces more versatile. But I don’t envy you having to carry it around.
        <<jump date_helmeppo_3_OrderFood>>
    -> Something I throw
        Helmeppo: Throw, huh? Interesting. But how many do you plan on carrying into battle?
        I mean, if that’s really what you want, I say go for it. But in a protracted fight, you might run out of knives.
        <<jump date_helmeppo_3_OrderFood>>
    -> Something that explodes
        He looks a little taken aback.
        Helmeppo: Well, that sounds… Effective. But I’m not sure anyone would want you on your ship if you’re tossing explosives around. Anything else?
        <<jump date_helmeppo_3_WeaponSelect>>
-> Let’s talk about something else.
    Helmeppo: Oh, yeah. Sorry for talking shop while we’re on down time.
    <<jump date_helmeppo_3_OrderFood>>
===

title: date_helmeppo_3_OrderFood
---
The waiter walks up with a pitcher in hand to top off the water glasses and asks if you’ve decided what you’d like to eat. Helmeppo orders a steak, medium rare, and potatoes. You order:
-> The same thing as him
    <<set $hd3_points += 1>>
    The waiter gets a knowing smile that seems inappropriate for the situation as he notes your order.
    <<jump date_helmeppo_3_Convo2>>
-> The cheapest thing on the menu
    <<set $hd3_points -= 1>>
    You catch Helmeppo make a move as if to say something, but then he just shakes his head and puts his menu to one side.
    <<jump date_helmeppo_3_Convo2>>
-> Your favorite food
    <<set $hd3_points += 1>>
    You glance through the menu and find something you loved from your childhood — something that never gets made in the mess halls of the Marines.
    <<jump date_helmeppo_3_Convo2>>
===

title: date_helmeppo_3_Convo2
characters: [helmeppo Waiter]
---
The waiter collects your menus and departs with your dinner order. Helmeppo sips his water thoughtfully.
Helmeppo: Thanks for coming out with me, by the way. Our last mission was kind of intense. I needed to unwind. I’d usually ask Coby, but he’s… Well, you’ve seen him.
Helmeppo sighs.
Helmeppo: He throws himself into danger for the greater good every. Single. Time. Including this last one. If he were here, I’d just end up lecturing him, and I…
He stops seeming to realize he’s been rambling. He coughs, looking flustered.
Helmeppo: I am clearly too wound up. So, to entirely change the topic… If you weren’t a Marine, what do you think you’d be doing now?
<<jump date_helmeppo_3_Answer1>>
===

title: date_helmeppo_3_Answer1
---
-> I think I’d work with animals.
    Helmeppo: Oh, interesting. Like training them, or fighting them, or…
    -> Training, for sure.
        Helmeppo: Oh, like horses, or dogs, or…
        -> Horses or other riding animals.
            Helmeppo: I suppose there’s always a need for that.
            His eyes widen abruptly.
            Helmeppo: I mean… Not to call it boring, or anything. I just meant… Uh…
            <<jump date_helmeppo_3_Convo3>>
        -> Dogs.
            Helmeppo: That sounds nice, but isn’t it better to just let the owner train the dog? Otherwise how would…
            He pauses, clearly rethinking what he’s saying, then trails off.
            <<jump date_helmeppo_3_Convo3>>
        -> I mean, whatever’s around. I’m not picky.
            Helmeppo: Really? That sounds less like a plan and more like you’re just…
            Helmeppo: Wait. [i]Is[/i] this a joke? I honestly can’t…
            He trails off, looking disconcerted.
            <<jump date_helmeppo_3_Convo3>>
    -> Fighting!
        Helmeppo smirks.
        Helmeppo: I knew it was just a matter of time until I met someone who wanted to fight bears or something for fun.
        Helmeppo: It honestly took longer than I thought it would.
        Helmeppo: I also was sure it would be Garp.
        -> Yeah, fighting a bear sounds like a pretty good test. Maybe a sea king.
            Helmeppo: A sea king? Isn’t that a little…
            He pauses, clearly rethinking whatever he was about to say. His face goes red.
            <<jump date_helmeppo_3_Convo3>>
        -> I meant more like hunting for food, but…
            Helmeppo: Oh… Yeah, that actually makes [i]way[/i] more sense, doesn’t it.
            <<jump date_helmeppo_3_Convo3>>
    -> In what world does “working with animals” mean “with my weapons”!?
        He opens his mouth, starts to speak, then closes it again.
        Helmeppo: You know what, that’s fair. I think being on the Grand Line screwed up my idea of what’s normal.
        <<jump date_helmeppo_3_Convo3>>
-> Helping people still, just in a different way.
    Helmeppo: Interesting. Any particular ways you’d want to help?
    -> Keeping the peace on a smaller scale.
        Helmeppo: Like being a guard for someone? Or more like a peacekeeper on an island?
        -> Bodyguard.
            Helmeppo: Huh. Really? That seems pretty-
            He cuts off, looking angry with himself.
            <<jump date_helmeppo_3_Convo3>>
        -> Peacekeeper.
            Helmeppo: Honestly, that feels like a better way to impact the world than being here. Though... Uh… Sorry.
            <<jump date_helmeppo_3_Convo3>>
    -> Building things. I like working with my hands.
        Helmeppo: Like a craftsperson?
        -> Maybe more like a farmer?
            Helmeppo: Like… Working in the dirt?
            He looks momentarily unsettled.
            Helmeppo: Er… I mean, if that’s something that would make your life happy, then… Yes. That’s… nice.
            <<jump date_helmeppo_3_convo3>>
        -> Yeah, a craftsperson.
            He takes another sip of his water before speaking again.
            Helmeppo: The world can always use more of those. But if you can do that, then why-
            He stops himself from finishing the question.
            <<jump date_helmeppo_3_Convo3>>
        -> More like construction.
            Helmeppo: Really? More power to you, but it sounds like a lot of work.
            You don’t think he means anything rude by that. But it kinda [i]sounds[/i] rude.
            <<jump date_helmeppo_3_Convo3>>
    -> Actually, on second thought…
        <<jump date_helmeppo_3_Answer1>>
-> I don’t think I could imagine doing something else.
    Helmeppo: Really? Nothing at all?
    -> Actually, on second thought…
        <<jump date_helmeppo_3_Answer1>>
    -> No. To reach my dreams, this is the best place for me.
        He smiles briefly.
        Helmeppo: Really? Bold declaration. But to be honest, I’m impressed.
        {$name}: Impressed?
        Helmeppo: I may not understand that level of conviction. But I envy it.
        -> Thanks?
            You both fall quiet.
            <<jump date_helmeppo_3_Convo3>>
        -> You don’t have anything you want that strongly?
            Helmeppo: Eh…
            He hesitates, then seems to steel himself.
            Helmeppo: Not in the way you mean. I guess my dreams aren’t really that concrete. Maybe someday.
            <<jump date_helmeppo_3_Convo3>>
===

title: date_helmeppo_3_Convo3
---
He seems to realize he’s said something awkward and apologizes with a blush, but the damage is done. The atmosphere has gotten a little awkward.
Luckily, the food arrives almost immediately after. For a couple minutes, the two of you eat mostly in uncomfortable silence while the restaurant hums around you. But after a bit, you can’t stand it anymore. You want to get some sort of conversation going again.
So, what do people talk about when they’re hanging out outside of work? Um…
-> What kind of music do you like?
    The question seems to catch him off guard.
    Helmeppo: Music? I don’t know. I don’t really listen to too much. There are a couple pop singers I like. And some stuff out of the West Blue.
    Helmeppo: How about you?
    -> I love pop and rock music!
        Helmeppo nods enthusiastically.
        Helmeppo: I love how it changes as newcomers develop their own styles. Who do you like?
        -> Soul King Brook.
            He raises one eyebrow.
            Helmeppo: You do know he’s a pirate, right?
            -> A pirate with the voice of a music god.
                <<set $hd3_points += 1>>
                Helmeppo laughs.
                Helmeppo: Can’t argue with that.
                <<jump date_helmeppo_3_GoodClose>>
            -> Wait, he [i]is[/i]?
                <<set $hd3_points += 1>>
                Helmeppo stares you down, clearly trying to figure out if you’re being serious or not. Whatever he decides, he starts laughing.
                <<jump date_helmeppo_3_FriendlyClose>>
            -> I choose to separate the art from the artist.
                Helmeppo: Fair enough. Just never, ever, let the Fleet Admiral hear you say that.
                <<jump date_helmeppo_3_FriendlyClose>>
        -> Uta.
            Helmeppo visibly pales.
            Helmeppo: Oh… Yeah? She has quite a voice, that’s for sure.
            -> You don’t like her music?
                Helmeppo: It’s good. I just have some bad associations, is all.
                <<jump date_helmeppo_3_FriendlyClose>>
            -> Yeah, her voice is so powerful. It makes me feel like I almost can’t help but dance!
                Helmeppo nods in agreement, but doesn’t say anything more.
                <<jump date_helmeppo_3_AwkwardClose>>
    -> Classical forms from any of the seas.
        Helmeppo smiles thoughtfully.
        Helmeppo: Classical huh? Gives you plenty of backlog to get through, at least. What drew you to that?
        -> My parents liked it.
            <<set $hd3_points += 1>>
            The smile softens a little.
            Helmeppo: Really? That’s very nice. Having something like that.
            <<jump date_helmeppo_3_FriendlyClose>>
        -> It motivated me when I was training.
            <<set $hd3_points += 1>>
            Helmeppo nods thoughtfully.
            Helmeppo: Music during training. I’ve never tried it, but it sounds like it could be really motivating.
            <<jump date_helmeppo_3_FriendlyClose>>
    -> Drinking songs. Emphasis on the drinking.
        Helmeppo laughs.
        Helmeppo: They really are inventive sometimes, aren’t they? Got a favorite?
        -> Captain Roger’s Staff.
            Helmeppo doesn’t say anything about the raunchy song, but his raised eyebrow speaks volumes.
            <<jump date_helmeppo_3_FriendlyClose>>
        -> The Wail and the Swell.
            Helmeppo: Heavy on the fiddle huh? Well, I guess it makes sense on the ocean. Not a lot of room for contra bass or piano on most ships.
            <<jump date_helmeppo_3_FriendlyClose>>
        -> Bink’s Sake.
            <<set $hd3_points += 1>>
            Helmeppo smiles fondly.
            Helmeppo: An old classic. Nice choice.
            <<jump date_helmeppo_3_FriendlyClose>>
-> Where would you want to go on vacation?
    He considers the question between bites of his meal.
    Helmeppo: Vacation. I haven’t thought about that in ages. A resort might be nice. Or maybe back to the Baratie. I’ve always wanted to try that place…
    -> A resort?
        Helmeppo: There are a few of them out there. I went to a couple as a kid, but I barely remember them.
        Helmeppo: It would be nice to see if they live up to my memory. All the weaker seas but East Blue have at least one. Which would you visit, if you could?
        -> North Blue.
            Helmeppo: The Lvneel Kingdom actually has a resort based around Nolan the Liar. Do you remember that old tale?
            Helmeppo: I feel like it’s more for kids, but it could be fun to visit.
            <<jump date_helmeppo_3_FriendlyClose>>
        -> West Blue.
            Helmeppo: I hear the fireworks over the ocean in the Ballywood kingdom are fantastic.
            <<jump date_helmeppo_3_FriendlyClose>>
        -> South Blue.
            Helmeppo: Down there, I’ve always wanted to go to the Samba Kingdom’s resort. I could do without all the dancing, but they say the weather is always perfect.
            <<jump date_helmeppo_3_FriendlyClose>>
    -> The Baratie sounds amazing to me as well.
        <<set $hd3_points += 1>>
        Something lights up in his eyes at realizing you know about the floating restaurant as well.
        Helmeppo: Right? My father went for meetings sometimes, but I was never allowed to go. I hear they have a steak that uses the most tender meat ever, and perfectly prepared by the head chef.
        Helmeppo: What has you interested in it?
        -> I’ve heard nothing but good things about the food too.
            Helmeppo nods enthusiastically.
            Helmeppo: Exactly! I have to see if we can swing by next time we’re on a mission out there. If we ever are.
            <<jump date_helmeppo_3_FriendlyClose>>
        -> The bar has some amazing unique cocktails that’ll even get a pirate tipsy.
            Helmeppo looks surprised.
            Helmeppo: Big drinker, huh? Well, I’ve heard the same. The place carries top booze from all the seas. If we go there next time, let’s top off the night there.
            He only seems to realize what he said after he says it.
            <<jump date_helmeppo_3_GoodClose>>
        -> Wait, your dad went multiple times and [i]never[/i] took you?
            He laughs a little and shrugs.
            Helmeppo: To be fair, I was a brat back then, instead of the finely tuned asshole I’ve become. I guess it was for the best.
            <<jump date_helmeppo_3_FriendlyClose>>
-> Does this feel awkward to you too, or is it just me?
    He looks up, and you realize that, somehow, you managed to ask that question just as he was taking a bite of his meal. It’s so awkward at this point, as you wait for him to finish chewing and speak, that you actually feel tempted to laugh.
    -> Go ahead and laugh.
        <<set $hd3_points += 1>>
        As he finished the bite, he smirked, then joined you with a chuckle of his own.
        Helmeppo: No, you’re definitely right, this was totally awkward. Sorry. That’s probably half my fault.
        -> At least half.
            He raises an eyebrow.
            Helmeppo: Hmm, yes. Passing the blame. Excellent instincts, I’m impressed.
            <<jump date_helmeppo_3_FriendlyClose>>
        -> [i]Way[/i] more than half.
            He considers that answer, then nods.
            Helmeppo: Yes, well, I am very adept at things like this. Or so everyone tells me.
            <<jump date_helmeppo_3_FriendlyClose>>
        -> No way, I’m claiming at least sixty percent of the awkwardness for myself.
            He holds up his hands in a fake surrender pose.
            Helmeppo: Hey, if you want to claim responsibility, I’m not going to stop you. Promise I’ll hold up my end of the bargain more next time.
            <<jump date_helmeppo_3_GoodClose>>
    -> Hold it in.
        Finally, he swallows, then shakes his head a bit sardonically.
        Helmeppo: Well, I’m glad someone said it.
        He laughs. You…
        -> Finally let it out.
            <<set $hd3_points += 1>>
            He seems a little surprised when you start laughing along with him, but then relaxes.
            Helmeppo: To be honest, I expect every interaction that’s not a battle to go awkwardly. And half the fights too, if I’m being honest.
            Helmeppo: So I guess if we’re going to keep hanging out, better get used to it, huh?
            -> Looking forward to it!
                <<jump date_helmeppo_3_GoodClose>>
            -> I guess so.
                <<jump date_helmeppo_3_FriendlyClose>>
        -> Keep holding it in.
            <<set $hd3_points -= 1>>
            His laughter trails off, punctuated by a little cough. He looks at you, evaluating, then returns to his meal again.
            <<jump date_helmeppo_3_AwkwardClose>>
        -> Wink at him.
            <<set $hd3_points += 1>>
            It seems the flirty gesture caught him a little off guard, and you earn another blush for your forwardness.
            Helmeppo: I really don’t know what you’re thinking half the time.
            Helmeppo: But I’m glad you agreed to come out with me today, for some reason.
            -> I’m glad too.
                <<jump date_helmeppo_3_GoodClose>>
            -> I’m not sure I’d want do this [i]again[/i], but it’s been fun.
                <<jump date_helmeppo_3_FriendlyClose>>
-> You can’t think of anything and go back to eating.
    <<set $hd3_points -= 1>>
    <<jump date_helmeppo_3_AwkwardClose>>
===

title: date_helmeppo_3_AwkwardClose
---
The two of you finish your meal in awkward silence.
<<jump date_helmeppo_3_GoHome2>>
===

title: date_helmeppo_3_FriendlyClose
---
After a short, thoughtful period, Helmeppo speaks.
Helmeppo: Hey, sorry about earlier. I’m still not the greatest at polite small-talk. Coby says I have a leash on everything but my snark. Thanks for not holding it against me.
The two of you finish your meal with inconsequential small talk.
<<jump date_helmeppo_3_GoHome2>>
===

title: date_helmeppo_3_GoodClose
---
After a short, thoughtful period, Helmeppo speaks.
Helmeppo: Hey, I know it’s a little late. But I wanted to say… Thanks. I was kind of rude about your dreams earlier. I shouldn’t have said something like that. But you either didn’t hold it against me or are waiting to get revenge until we’re out of my favorite restaurant. Either way, I appreciate it.
The two of you continue talking about all manner of things as you finish your meal.
<<jump date_helmeppo_3_GoHome2>>
===

title: date_helmeppo_3_GoHome1
---
Helmeppo: All right, you can’t say I didn’t offer.
<<jump date_helmeppo_3_EndDate>>
===

title: date_helmeppo_3_GoHome2
---
True to his word, he pays for both of your meals. You head out of the restaurant and back toward your more typical haunts, until you get to where you each need to go your separate ways.
<<jump date_helmeppo_3_EndDate>>
===

title: date_helmeppo_3_EndDate
// TODO: Replace with better location
location: night(mess_hall)
---
You wave goodbye, ready to head back to the rest of your day.
<<if $hd3_points < 0>>
    <<romance_points helmeppo 0>>
    Helmeppo: Bye. And I’m calling us even now.
<<elseif $hd3_points <= 3>>
    <<romance_points helmeppo 1>>
    Helmeppo: I hope that was adequate payment for humoring me the other day. And I hope you had fun. I did.  (+1 point)
<<else>>
    <<romance_points helmeppo 2>>
    He waves back and smiles a little, but he looks a little uncomfortable as well.
    Helmeppo: I appreciate… No. I’m glad you agreed to come with me. I had a surprisingly good time. Not that it’s surprising because of you, just… Ah…
    He blushes a little and turns abruptly, hurrying off. (+2 points)
<<endif>>

<<jump date_helmeppo_4_main>>
===
